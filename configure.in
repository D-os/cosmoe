dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/kits/interface/View.cpp)
AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl Determine our platform.
AC_CANONICAL_HOST

dnl Checks for libraries.
AC_CHECK_LIB(stdc, printf)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC

case "$host" in
	*darwin*)	LIBEXT="dylib"
				SHAREDLINK="-dynamiclib"
				STRCASESTROBJ="string_helper.o"
				FONTDIR="/Library/Fonts/cosmoe"
				;;
	*)			LIBEXT="so"
				SHAREDLINK="-shared"
				STRCASESTROBJ=
				FONTDIR="/usr/share/fonts/ttf/cosmoe"
				;;
esac

ABS_TOPDIR=`pwd`

AC_CHECK_LIB(pthread, pthread_kill, ,
  AC_MSG_ERROR([
*** Cosmoe requires a libpthread with pthread_kill support.]))

AC_CHECK_LIB(dl, dlopen, ,
  AC_MSG_ERROR([
*** Cosmoe requires libdl.]))

AC_CHECK_HEADER(png.h,
  AC_DEFINE(COSMOE_PNG, [], [PNG libraries/headers are available]),
  AC_MSG_WARN([
*** libjpeg header files not found.  PNG translator will not be built.]))

AC_CHECK_LIB(png, png_read_info, ,
  AC_MSG_ERROR([
*** Cosmoe requires libpng.]),
  -lz -lm)

AC_CHECK_HEADER(zlib.h, ,
  AC_MSG_ERROR([
*** Cosmoe requires the libz header files to support libpng.]))

AC_CHECK_LIB(jpeg, jpeg_destroy_decompress, ,
  AC_MSG_ERROR([
*** Cosmoe requires libjpeg.]),
  -lz -lm)

AC_CHECK_HEADER(jpeglib.h,
  AC_DEFINE(COSMOE_JPEG, [], [JPEG libraries/headers are available]),
  AC_MSG_WARN([
*** libjpeg header files not found.  JPEG translator will not be built.]))

AC_CHECK_HEADER(sys/xattr.h,
  AC_DEFINE(COSMOE_ATTRIBUTES, [], [OS has generic filesystem attribute support]),
  AC_MSG_WARN([
*** This build of Cosmoe will not support attributes.]))

AC_PATH_PROG(FREETYPE_CONFIG, freetype-config, no)
if test "$FREETYPE_CONFIG" = no; then
    AC_MSG_ERROR([
*** Cosmoe requires libfreetype version 2.0 or later.])
fi


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL

dnl Checks for sem_init that works with the pshared option.
AC_MSG_CHECKING(for working process-shared sem_init)
AC_TRY_RUN([
#include <errno.h>
#include <stdlib.h>
#include <fcntl.h>
#include <semaphore.h>
main()
{
    sem_t sem;
    int err;

    err = sem_init(&sem, 1, 0);
    if (err < 0)
        exit(1);
    
    sem_destroy(&sem);
    exit(0);
}],
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_WORKING_POSIX_SEMS, [], [OS has process-shared POSIX semaphore support])
    ,
    AC_MSG_RESULT(no))

dnl It's stupid, but not all platforms have union semun, even those that need it.
AC_MSG_CHECKING(for union semun in sys/sem.h)
AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/ipc.h>
#include <sys/sem.h>
],[
union semun arg;
semctl(0, 0, 0, arg);
],
[have_union_semun="1" msg=yes], 
[have_union_semun="0" msg=no])
AC_MSG_RESULT([$msg])
AC_DEFINE(HAVE_UNION_SEMUN, [], [union semun is defined])

VIDEODRVLIB=""
VESAOBJ=""

AC_MSG_CHECKING(whether to enable DirectFB graphics rendering)
AC_ARG_ENABLE(directfb, [  --enable-directfb       run Cosmoe on top of DirectFB [default=no]],
 if eval "test x$enable_directfb = xyes"; then
   VIDEODRVLIB=`directfb-config --libs`
   VIDEODRVOBJ="dfbdriver.o"
   VIDEODRVCFLAGS=`directfb-config --cflags`
   AC_DEFINE(COSMOE_DIRECTFB, [], [Use DirectFB for graphics output])
   AC_MSG_RESULT(yes)
 else
   AC_MSG_RESULT(no)
 fi
 , AC_MSG_RESULT(no))

AC_MSG_CHECKING(whether to enable XWindows graphics rendering)
AC_ARG_ENABLE(x, [  --enable-x              run Cosmoe in an X window [default=no]],
 if eval "test x$enable_x = xyes"; then
   VIDEODRVLIB="-L/usr/X11R6/lib -lX11"
   VIDEODRVOBJ="x11driver.o"
   VIDEODRVCFLAGS=""
   AC_DEFINE(COSMOE_XWINDOWS, [], [Use XWindows for graphics output])
   AC_MSG_RESULT(yes)
 else
   AC_MSG_RESULT(no)
 fi
 , AC_MSG_RESULT(no))

AC_MSG_CHECKING(whether to enable SDL graphics rendering)
dnl Default to SDL graphics if another graphics method was not chosen
if test -z "$VIDEODRVLIB"; then
   VIDEODRVLIB=`sdl-config --libs`
   VIDEODRVOBJ="sdldriver.o"
   VIDEODRVCFLAGS=`sdl-config --cflags`
   AC_DEFINE(COSMOE_SDL, [], [Use SDL for graphics output])
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi

AC_SUBST(VIDEODRVOBJ)
AC_SUBST(VIDEODRVLIB)
AC_SUBST(VIDEODRVCFLAGS)
AC_SUBST(VESAOBJ)
AC_SUBST(LIBEXT)
AC_SUBST(SHAREDLINK)
AC_SUBST(STRCASESTROBJ)
AC_SUBST(HAS_ATTR_SUPPORT)
AC_SUBST(FONTDIR)

AC_DEFINE(COSMOESYS, "/usr/local/share/cosmoe", [Location of Cosmoe-specific files])
COSMOESYS="/usr/local/share/cosmoe"
AC_SUBST(COSMOESYS)

AC_SUBST(ABS_TOPDIR)

AC_OUTPUT([
Makefile
cosmoe.specs
headers/Makefile
system/Makefile
src/kits/Makefile
src/apps/Makefile
src/apps/bedate/Makefile
src/apps/guido/Makefile
src/apps/calc/Makefile
src/apps/OpenTracker/tracker/Makefile
src/apps/OpenTracker/deskbar/Makefile
src/apps/pulse/Makefile
src/apps/cterm/Makefile
src/apps/desktop/Makefile
src/apps/testharness/Makefile
src/apps/osutils/Makefile
src/servers/registrar/Makefile
src/servers/app/Makefile
src/add-ons/Makefile
src/add-ons/decorators/Makefile
src/add-ons/decorators/be/Makefile
src/add-ons/decorators/next/Makefile
src/add-ons/decorators/amiga/Makefile
src/add-ons/translators/Makefile
src/add-ons/translators/bmptranslator/Makefile
src/add-ons/translators/pngtranslator/Makefile
])

dnl add-ons/translators/jpg2000translator/Makefile
dnl add-ons/translators/jpgtranslator/Makefile
dnl add-ons/translators/libtifftranslator/Makefile
dnl add-ons/translators/pngtranslator/Makefile
dnl add-ons/translators/ppmtranslator/Makefile
dnl add-ons/translators/sgitranslator/Makefile
dnl add-ons/translators/stxttranslator/Makefile
dnl add-ons/translators/tgatranslator/Makefile
dnl add-ons/translators/tifftranslator/Makefile
